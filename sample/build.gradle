/*
 * Copyright (C) 2016 Samuel Wall
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'jacoco'
apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "uk.co.samuelwall.materialtaptargetprompt.sample"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:' + rootProject.supportLibVersion
    compile 'com.android.support:support-v4:' + rootProject.supportLibVersion
    compile 'com.android.support:design:' + rootProject.supportLibVersion
    compile 'com.android.support:cardview-v7:' + rootProject.supportLibVersion
    compile 'com.android.support:recyclerview-v7:' + rootProject.supportLibVersion
    compile project('path': ':material-tap-target-prompt', configuration: 'libraryRelease')
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude module: 'support-annotations'
    }
    androidTestCompile project('path': ':material-tap-target-prompt', configuration: 'librarytoolsRelease')
}


jacoco {
    toolVersion = "0.7.6+"
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'createDebugCoverageReport') {

    reports {
        xml.enabled = false
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    def libraryDebugTree = fileTree(dir: "${project.rootDir}../library/build/intermediates/classes/debug", excludes: fileFilter)
    def librarySrc = "${project.rootDir}../library/src/main/java"

    additionalSourceDirs = files([mainSrc, librarySrc])
    sourceDirectories = files([mainSrc, librarySrc])
    classDirectories = files([debugTree, libraryDebugTree])
    executionData = fileTree(dir: "$buildDir", includes: ["outputs/code-coverage/connected/*coverage.ec"])
}
